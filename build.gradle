plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.vicious'
version = '1.3.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.jetbrains:annotations:24.0.0'
    implementation 'org.jetbrains:annotations:24.0.0'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(8)
}

publishing {
    repositories {
        maven {
            name = "Persist"
            url = uri("https://maven.pkg.github.com/Drathonix/Persist")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = "com.vicious"
            artifactId = 'persist'
            version = "${version}"
            from components.java
        }
    }
}